# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: xantares <xantares09@hotmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

pkgname=popcorntime
pkgver=0.3.4
pkgrel=8
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64' 'i686')
url="http://popcorntime.io/"
license=('GPL3')
makedepends=('nodejs-grunt-cli' 'git' 'nodejs-bower')
depends=('ttf-ms-fonts' 'gconf')
options=('!strip')
install="popcorntime.install"
_gitname=popcorn-app
md5sums=('efe872f41ed74a706308b4015e4fd82f'
         '9608f35f4cc083a0c36b0f4ef275ab10'
         'SKIP')

if [ "$CARCH" = 'i686' ]; then
  _platform=linux32
elif [ "$CARCH" = 'x86_64' ]; then
  _platform=linux64
fi

source=("popcorntime.install"
        "popcorntime.desktop"
        "git+https://git.popcorntime.io/stash/scm/pt/popcorn-app.git#tag=v$pkgver")

prepare() {
  export PYTHON=/usr/bin/python2
  cd "${srcdir}/${_gitname}"

  # Get dependencies
  npm install
}

build() {
  cd "${srcdir}/${_gitname}"
  grunt build

  # Thanks to Revelation60 for pointing it out
  # https://github.com/rogerwang/node-webkit/wiki/The-solution-of-lacking-libudev.so.0
  msg2 "Patching program to fix libudev.so.0 problem"
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"
  sed -i 's/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x30/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x31/g' Popcorn-Time
}

package() {
  cd "${srcdir}"

  _bpath="${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  # Program
  install -Dm755 "${_bpath}/Popcorn-Time" "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 "${_bpath}/nw.pak" "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 "${_bpath}/libffmpegsumo.so" "${pkgdir}/usr/share/${pkgname}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${pkgname}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/${_gitname}/src/app/images/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}

# vim:set ts=2 sw=2 et:
