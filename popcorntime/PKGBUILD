# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

pkgname=popcorntime
pkgver=2.5
_pkgbuild=beta_2_5
pkgrel=2
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64')
url="http://getpopcornti.me/"
license=('custom')
makedepends=('zip')
conflicts=('popcorntime-beta' 'popcorntime-git')
options=('!strip')
source=("https://github.com/popcorn-time/popcorn-app/archive/${_pkgbuild}.tar.gz"
        "popcorntime.png"
        "popcorntime.desktop"
        "LICENSE")
md5sums=('471cc8026809622ed1e8c621c3dbaad1'
         'c8a03cc6b0f7af7404d1686b53923bd8'
         '9608f35f4cc083a0c36b0f4ef275ab10'
         '1e6df124be80e43f3f819b832560432e')

build() {
  cd "${srcdir}/popcorn-app-${_pkgbuild}/dist/linux"
  sed -i '$ d' build.sh
  sh build.sh
}

package() {
  cd "${srcdir}/popcorn-app-${_pkgbuild}/dist/linux/PopcornTime"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"

  # Program
  install -Dm755 popcorn-app.run "${pkgdir}/usr/share/${pkgname}/"
  install -Dm744 nw.pak "${pkgdir}/usr/share/${pkgname}/"
  install -Dm755 libffmpegsumo.so "${pkgdir}/usr/share/${pkgname}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname}/popcorn-app.run" "${pkgdir}/usr/bin/${pkgname}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/popcorntime.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"

  # License
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
