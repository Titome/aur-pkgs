# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Ricardo Band <me [at] xengi [dot] de>

pkgname=popcorntime
pkgver=0.2.7
pkgrel=4
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64' 'i686')
url="http://popcorn.cdnjd.com/"
license=('GPL3')
makedepends=('zip')
depends=('libudev.so.0' 'ttf-ms-fonts')
conflicts=('popcorntime-beta' 'popcorntime-git')
options=('!strip')
install="popcorntime.install"
md5sums=('SKIPPED'
         '5725b4c466ca3726e7a6df52730dacd2'
         '8dd1d388ccbab5ad5d58185310289b5b'
         '9608f35f4cc083a0c36b0f4ef275ab10')

if [ "$CARCH" = 'i686' ]; then
  _platform=Linux-32
  md5sums[0]='14df67696694caeb865569bcae28d4b7'
elif [ "$CARCH" = 'x86_64' ]; then
  _platform=Linux-64
  md5sums[0]='0173efaffed2bc867888650af52b19ca'
fi

source=("http://popcorn.cdnjd.com/releases/Popcorn-Time-2.7-${_platform}.tgz"
        "https://raw.githubusercontent.com/popcorn-team/popcorn-app/v${pkgver}-beta/images/icon.png"
        "popcorntime.install"
        "popcorntime.desktop")

package() {
  cd "${srcdir}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  # Program
  install -Dm755 Popcorn-Time "${pkgdir}/usr/share/${pkgname}/"
  install -Dm744 nw.pak "${pkgdir}/usr/share/${pkgname}/"
  install -Dm755 libffmpegsumo.so "${pkgdir}/usr/share/${pkgname}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/usr/share/${pkgname}/Popcorn-Time" "${pkgdir}/usr/bin/${pkgname}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/icon.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}
