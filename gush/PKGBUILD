# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>

pkgname=gush
pkgver=1.3.10
pkgrel=1
pkgdesc="Rapid workflow for GitHub project maintainers and contributors"
url="http://gushphp.org"
arch=('any')
license=('MIT')
makedepends=('php-box' 'php-composer')
depends=('php')
source=("https://github.com/gushphp/gush/archive/${pkgver}.tar.gz"
        'box.json.dist.patch')
md5sums=('1363c1b6235973b848173a8295401c73'
         '9e8c20108b76a59a3461f7a2fabba290')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  composer install --no-dev
  sed -i "s/@package_version@/${pkgver}/" "${srcdir}/${pkgname}-${pkgver}/src/Gush/Application.php"
  patch -p0 <"${srcdir}/box.json.dist.patch"
  php-box build
  ./gush.phar --version
}

package() {
  install -D -m 755 "${srcdir}/gush-${pkgver}/gush.phar" "${pkgdir}/usr/share/webapps/bin/gush.phar"
  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/webapps/bin/gush.phar "${pkgdir}/usr/bin/gush"
}

# vim:set ts=2 sw=2 et:
