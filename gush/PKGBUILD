# Maintainer: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>

pkgname=gush
pkgver=1.5.0
pkgrel=1
pkgdesc="Rapid workflow for GitHub project maintainers and contributors"
url="http://gushphp.org"
arch=('any')
license=('MIT')
makedepends=('php-box' 'php-composer')
depends=('php')
source=("https://github.com/gushphp/gush/archive/${pkgver}.tar.gz"
        'box.json.dist.patch')
md5sums=('b4c1900455313cfc6793a271f24ccd43'
         '32a4b5934cce44a8c8a1fe01de830262')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  composer install --no-dev
  sed -i "s/@package_version@/${pkgver}/" "${srcdir}/${pkgname}-${pkgver}/src/Application.php"
  patch -p0 <"${srcdir}/box.json.dist.patch"
  php-box build
  ./gush.phar --version
}

package() {
  install -D -m 755 "${srcdir}/gush-${pkgver}/gush.phar" "${pkgdir}/usr/share/webapps/bin/gush.phar"
  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/webapps/bin/gush.phar "${pkgdir}/usr/bin/gush"
}

# vim:set ts=2 sw=2 et:
