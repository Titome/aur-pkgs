# Maintainer: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=apache-cxf
pkgver=2.7.11
pkgrel=1
pkgdesc='open source services framework'
arch=('any')
license=('Apache')
url='http://cxf.apache.org'
makedepends=('maven')
depends=('java-runtime')
source=("http://www.us.apache.org/dist/cxf/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        'apache-cxf.sh')
_distdir="${pkgname}-${pkgver}-src/distribution/src/main/release"
md5sums=('8e2734c1663baa1703044f0b7ec6d6d8'
         '94fa4d0c0f7f2ba93c30eee05e644c7a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  export MAVEN_OPTS="-XX:MaxPermSize=192m -Xmx768M"
  mvn -Pfastinstall
}

package() {
  install -m755 -d \
    "${pkgdir}/usr/share/${pkgname}/lib" \
    "${pkgdir}/usr/bin" \
    "${pkgdir}/etc/profile.d"

  cp -r "${srcdir}/${_distdir}/lib/"* "${pkgdir}/usr/share/${pkgname}/lib"

  cp -r "${srcdir}/${_distdir}/bin" "${pkgdir}/usr/"
  find "${pkgdir}/usr/bin/" -name '*.bat' -delete

  # to resolve conflict between midnight commander and apache-cxf
  mv "${pkgdir}/usr/bin/mc" "${pkgdir}/usr/bin/apache-mc"

  install -m 644  "${srcdir}/apache-cxf.sh" "${pkgdir}/etc/profile.d"
}

# vim: ts=2 sw=2 et
